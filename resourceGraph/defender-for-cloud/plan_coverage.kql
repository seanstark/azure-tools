securityresources
| where type =~ 'microsoft.security/pricings'
| extend planName = name
| extend freeTrialRemainingTime = properties.freeTrialRemainingTime
| extend resourcesCoverageStatus = tostring(properties.resourcesCoverageStatus)
| extend isEnabled = case (
    tostring(properties.pricingTier) == 'Standard', 'Enabled',
    'Disabled'
)
| extend isPlanDeprecated = case (
    properties.deprecated != true, 'false',
    tostring(properties.deprecated)
)
| extend subPlan = case(
        isnotempty(properties.subPlan), tostring(properties.subPlan),
        'base'
    )
| extend newPlanAvailable = case (
    subPlan in ('PerApiCall','PerTransaction'), 'true',
    isPlanDeprecated == 'true', 'true',
    'false'
)
| extend planSet = tolower(strcat(planName, '-', subPlan))
| extend newPlanName = case ( 
    planName in ('ContainerRegistry', 'KubernetesService'), 'Defender for Containers',
    planName == 'Dns', 'Defender for Servers P2',
    planSet == 'keyvaults-pertransaction', 'Defender for Key Vault-perkeyvault',
    planSet == 'storageaccounts-pertransaction', 'Defender for Storage-peraccount',
    planSet == 'arm-perapicall', 'Defender for Resource Manager-persubscription',
    'n/a'
)
| extend isPlanDeprecated = case (
    newPlanAvailable == 'true', 'true',
    properties.deprecated != true, 'false',
    tostring(properties.deprecated)
)
| extend extensions = tostring(properties.extensions)
| project subscriptionId, planName, planSet, freeTrialRemainingTime, resourcesCoverageStatus, isEnabled, isPlanDeprecated, subPlan, newPlanAvailable, newPlanName, extensions
| order by ['subPlan'] asc
